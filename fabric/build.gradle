plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${mod_id}-fabric-${minecraft_version}"

repositories {
    maven {
        name = "Ladysnake Libs"
        url = "https://ladysnake.jfrog.io/artifactory/mods"
    }

    maven {
        url = "https://maven.shedaniel.me/"
        name = "shedaniel"
    }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
    }
}


dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "dev.emi:trinkets:${project.trinkets_version}"
    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}") {
        exclude(group: "dev.onyxstudios.cardinal-components-api")
    }

    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1")

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation project(":shared")
}

loom {
    accessWidenerPath = file("src/main/resources/voidtotem.accesswidener")

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
        }
    }
}


processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":shared").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
